import QtQuick
import QtQuick.Controls.Basic

Button {
    id: control
    property bool primary: false
    property bool selected: false
    property bool danger: false
    implicitHeight: Math.max(theme.height, contentItem.implicitHeight + 14)
    implicitWidth: Math.max(80, contentItem.implicitWidth + leftPadding + rightPadding)
    leftPadding: 16; rightPadding: 16
    topPadding: 7; bottomPadding: 7
    font.pixelSize: theme.body; font.weight: primary ? Font.DemiBold : Font.Medium
    hoverEnabled: true
    opacity: enabled ? 1 : 0.42
    contentItem: Text {
        text: control.text; font: control.font
        color: control.primary ? theme.bg : control.danger ? theme.danger : control.selected ? theme.accent
             : control.flat && !control.hovered ? theme.muted : theme.text
        horizontalAlignment: Text.AlignHCenter; verticalAlignment: Text.AlignVCenter
        wrapMode: Text.Wrap
        Behavior on color { ColorAnimation { duration: 110 } }
    }
    background: Rectangle {
        radius: theme.radiusSm
        color: control.primary ? (control.down ? theme.accentDim : control.hovered ? theme.accentHover : theme.accent) :
               control.selected ? theme.accentSubtle : control.down ? theme.sheen : control.hovered ? theme.hover :
               control.flat ? "transparent" : theme.raised
        border.width: control.activeFocus && control.visualFocus ? 2 : control.primary || control.flat ? 0 : 1
        border.color: control.activeFocus && control.visualFocus ? theme.accentHover : control.selected ? theme.accentDim : theme.border
        Behavior on color { ColorAnimation { duration: 110 } }
    }
    Accessible.name: text
}
